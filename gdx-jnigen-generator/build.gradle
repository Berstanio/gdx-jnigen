buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url("https://oss.sonatype.org/content/repositories/snapshots/") }
    }

    dependencies {
        classpath "com.badlogicgames.gdx:gdx-jnigen-gradle:2.5.1"
    }
}

plugins {
    id 'java'
}

apply plugin: "com.badlogicgames.gdx.gdx-jnigen"

group = 'com.badlogicgames.gdx'
version = '2.5.1'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.bytedeco:llvm-platform:16.0.4-1.5.9'
    implementation 'com.github.javaparser:javaparser-core:3.23.1'
    implementation project(':gdx-jnigen-loader')
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation fileTree(dir: project(':gdx-jnigen-loader').file("build/libs/"), include: '*natives-desktop.jar')
}

task generateTestClasses(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = "com.badlogic.gdx.jnigen.generator.Generator"
    args file("src/test/java").absolutePath, "com.badlogic.jnigen.generated", file("src/test/resources/definitions.h").absolutePath
}

compileTestJava.dependsOn(generateTestClasses)

test {
    dependsOn(":gdx-jnigen-loader:jnigenBuild", ":gdx-jnigen-loader:jnigenJarNativesDesktop")
    useJUnitPlatform()
    outputs.upToDateWhen {false}
}

jnigen {
    sharedLibName = "test-natives"

    nativeCodeGenerator {
        it.sourceSet = sourceSets.named("test").get()
    }

    all {
//        headerDirs += [file("build/${combined}/include/"), file("src/test/native/"), file("src/main/resources/")]
//        libraries += "${file("build/${combined}/lib/libffi.a").absolutePath}"
        cFlags += " -std=c11 "
        cppFlags += " -std=c++11 "
        linkerFlags += " -Wl,-undefined,dynamic_lookup "
        cppCompiler = "clang"
    }

    add(Linux, x64)
    add(MacOsX, x64, ARM)
}